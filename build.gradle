plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'org.sonarqube' version '2.7'
}

group = 'com.iexec.common'
sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://jitpack.io"
    }
}

configurations {
    deployerJars
}

dependencies {
    // api dependencies will be exposed to
    // projects using this lib
    
    // lombok
    api "org.projectlombok:lombok:1.18.2"
    annotationProcessor "org.projectlombok:lombok:1.18.2"

    // swagger
    api "io.springfox:springfox-swagger2:2.9.2"
    api "io.springfox:springfox-swagger-ui:2.9.2"

    // web3j
    api "org.web3j:core:4.7.0"
    
    // apache commons.lang3
    api "org.apache.commons:commons-lang3:3.9"

    // multiaddresses (IPFS)
    api 'com.github.multiformats:java-multiaddr:1.3.1'

    // json
    api 'org.json:json:20201115'

    // google core libs
    api 'com.google.guava:guava:29.0-jre'

    //jaxb required with Java 11
    implementation 'javax.xml.bind:jaxb-api:2.3.0'

    //args utils
    implementation 'org.apache.maven.shared:maven-shared-utils:3.2.1'

    // zip
    implementation 'net.lingala.zip4j:zip4j:2.3.1'

    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'io.github.openfeign:feign-okhttp:11.0'
    implementation 'io.github.openfeign:feign-jackson:11.0'
    implementation 'io.github.openfeign:feign-slf4j:11.0'

    // docker
    implementation 'com.github.docker-java:docker-java:3.2.5'
    implementation 'com.github.docker-java:docker-java-transport-httpclient5:3.2.5'

    // test
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
    testImplementation 'org.mockito:mockito-core:3.6.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.6.0'
    testImplementation 'org.assertj:assertj-core:3.18.1'

    // package cloud
    deployerJars "io.packagecloud.maven.wagon:maven-packagecloud-wagon:0.0.6"
}

ext {
    // ./gradlew build -Pdev
    isDev = project.hasProperty('dev')
    gitBranch = 'git name-rev --name-only HEAD'.execute().text.trim()
    isMasterBranch = gitBranch == "master"
    isDevelopBranch = gitBranch == "develop"
    canUploadArchives = isMasterBranch || isDevelopBranch
    isSnapshotVersion = project.version.contains("SNAPSHOT")
}

test {
    useJUnitPlatform {
        if (isDev) {
            excludeTags 'slow'
        }
    }
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

jacoco {
    toolVersion = "0.8.3"
}
build.dependsOn jacocoTestReport

jar {
    // include source code in the jar
    from sourceSets.main.allSource
}

shadowJar { //required if we want to embed web3j dependency inside
    baseName = rootProject.name
    from sourceSets.main.allSource
    version = version
}
build.dependsOn(shadowJar)

project.ext.getNexusMaven = {
    def nexusMavenBase = "https://nexus.iex.ec/repository"
    if (isSnapshotVersion) {
        return nexusMavenBase + "/maven-snapshots/"
    } else {
        return nexusMavenBase + "/maven-releases/"
    }
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: getNexusMaven()) {
            authentication(userName: nexusUser, password: nexusPassword)
        }
    }
}

uploadArchives.enabled = canUploadArchives